ACTOR TheCrucible : BrutalWeapon replaces Crucible {
    obituary "%o was choped down by %k's Crucible."
    Inventory.PickupSound "AXEDRAW"
    Inventory.Pickupmessage "You got something that looks too big to be called a sword!"
    Inventory.Icon "CRUCA0"

    DamageType Cut
    Tag "The Crucible"
    Weapon.SlotPriority 9999
    Weapon.SlotNumber 1
    Weapon.KickBack 100
    Weapon.AmmoUse 0
	Weapon.AmmoGive 3
	Weapon.AmmoType "RedEnergyCharge"
    Scale 0.6

    +WEAPON.NOAUTOAIM
    //+WEAPON.NOAUTOFIRE  // disable the alt fire
    +FORCEXYBILLBOARD
    +WEAPON.NOALERT
    +WEAPON.AXEBLOOD
    +WEAPON.CHEATNOTWEAPON
    +WEAPON.AMMO_OPTIONAL

    States
    {
    Spawn:
        TNT1 A 0 A_CheckSight("Spawn2")
        //CRUC B 10
        CRUC BBBBBB 2 A_SpawnItem("CrucibleSpawnRedFlare",0,10)
        loop

    Spawn2:
        CRUC B 20
        Goto Spawn

    Ready:
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        TNT1 A 0
        CRST A 15
        TNT1 A 0 A_StartSound("crucible/raise", CHAN_AUTO)
        TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
        TNT1 A 0 A_TakeInventory("CrucibleAlt",1)
        CRST BCDEF 1
        TNT1 A 0 A_Quake(3, 5, 0, 32)
        Goto Ready2
    
    Ready2:
    Ready3:
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        CRID ABCDE 2 A_WeaponReady
        TNT1 A 0 A_StartSound("crucible/idle", CHAN_AUTO)
        TNT1 A 0 A_TakeInventory("CrucibleAlt",1)

        TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
        loop

    Deselect:
        TNT1 A 0 A_TakeInventory("CrucibleAlt",1)
        TNT1 A 0 A_StartSound("crucible/down", CHAN_AUTO)
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto Deselect2
        CRST FEDCB 1

    Deselect2:
        CRST A 1 A_Lower
        loop

    Select:
        TNT1 A 0
        TNT1 A 0 A_Giveinventory("GoSpecial",1)
        TNT1 A 0 A_Takeinventory("FistsSelected",1)
        TNT1 A 0 A_Takeinventory("SawSelected",1)
        TNT1 A 0 A_Takeinventory("ShotgunSelected",1)
        TNT1 A 0 A_Takeinventory("SSGSelected",1)
        TNT1 A 0 A_Takeinventory("MinigunSelected",1)
        TNT1 A 0 A_Takeinventory("PlasmaGunSelected",1)
        TNT1 A 0 A_Takeinventory("RocketLauncherSelected",1)
        TNT1 A 0 A_Takeinventory("GrenadeLauncherSelected",1)
        TNT1 A 0 A_Takeinventory("BFGSelected",1)
        TNT1 A 0 A_Takeinventory("BFG10kSelected",1)
        TNT1 A 0 A_Takeinventory("RailGunSelected",1)
        TNT1 A 0 A_Takeinventory("SubMachineGunSelected",1)
        TNT1 A 0 A_Takeinventory("RevenantLauncherSelected",1)
        TNT1 A 0 A_Takeinventory("LostSoulSelected",1)
        TNT1 A 0 A_Takeinventory("FlameCannonSelected",1)
        TNT1 A 0 A_Takeinventory("HasBarrel",1)
        CRST A 1 A_Raise
        loop

    Fire:
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        CRAT A 1
        CRA2 A 1
        CRA2 B 1
        CRA2 C 1
        TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
        CRAT B 1
        CRAT G 1 A_FireProjectile("CrucibleAttack", 0, 0, 0, 0, 0, 0)
        CRAT HIJ 1
        CRAT K 1
        CRAT A 0 A_ReFire
        Goto Ready2
        
    AltFire:
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        TNT1 A 0 A_JumpIfInventory("CrucibleAlt",1,"AltFire2")
        TNT1 A 0 A_TakeInventory("RedEnergyCharge", 2, 0)
        CRBL AB 1
        Goto AltFire2
        
    AltFire2:
        TNT1 A 0 A_GiveInventory("CrucibleAlt",1)
        TNT1 A 0 A_StartSound("crucible/idle", CHAN_AUTO)
        TNT1 A 0 A_GiveInventory("CrucibleBlock",1)
        CRBL CDED 3
        Goto Ready2

    NoCharges:
        CRST A 15 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, "Ready")
        Loop
    }
}

Actor CrucibleAttack {
    Projectile
    Radius 10
    Height 2
    DamageType Cut
    +MISSILE
    +FORCEXYBILLBOARD
    +FORCERADIUSDMG
    +EXTREMEDEATH
    +FORCEPAIN
    +NODAMAGETHRUST
    RenderStyle Add
    Alpha 0.6
    Damage 0
    Speed 0
    SeeSound "none"
    DeathSound "none"
    decal "none"
    Scale 0.01

    States {
        Spawn:
        Melee:
        Crash:
        Death:
            TNT1 A 0
            TNT1 A 0 A_AlertMonsters(200)
            TNT1 A 10 ACS_NamedExecuteAlways("CrucibleStrike", 0, A_Explode(random(100, 200), 60, 0, false, 60))
            Stop
        
        Hit:
            TNT1 A 0
            TNT1 A 0 A_StartSound("weapons/gswing", CHAN_AUTO)
            //TNT1 A 0 A_StartSound("crucible/fire", CHAN_AUTO)
            TNT1 A 0 A_TakeInventory("RedEnergyCharge", 1, 0, AAPTR_TARGET)
            Stop
    }
}

// AMMO

Actor RedEnergyCharge : Ammo
{
    Inventory.PickupMessage "Picked up 3 red energy charge"
    Inventory.Amount 3
    Inventory.MaxAmount 30
    Inventory.Icon "ARENA0"
    Ammo.BackpackAmount 3
    Ammo.BackpackMaxAmount 3
    Scale 0.5

	+INVENTORY.UNDROPPABLE
	+INVENTORY.PERSISTENTPOWER
    +INVENTORY.KEEPDEPLETED

    states
    {
    Spawn:
        //AREN AAAAAA 2 A_SpawnItem("RedEnergySpawnRedFlare",0,10)
        AREN ABCABC 2 A_SpawnItem("RedEnergySpawnRedFlare",0,2)
        loop
    }
}

// PLAYER

ACTOR TheDoomSlayer : BDoomer
{
    +FRIGHTENING
    Speed 1.0
    Health 200
	Player.DisplayName "The Doom Slayer"

    // startitem are not inherited
	Player.StartItem "TheCrucible"
    Player.StartItem "RedEnergyCharge", 30
	Player.StartItem "Rifle"
	Player.StartItem "Melee_Attacks"
	Player.StartItem "IsPlayer", 1
	Player.StartItem "Clip2", 30
	Player.StartItem "RifleAmmo", 31
	Player.StartItem "BDPistolAmmo", 16
	Player.StartItem "BDDualPistolAmmo", 16
	Player.StartItem "DoubleRifleAmmo", 31
	Player.StartItem "ShotgunAmmo", 10
	Player.StartItem "PlasmaAmmo", 50
	Player.StartItem "DoublePlasmaAmmo", 50
	Player.StartItem "AssaultShotgunAmmo", 20
	Player.StartItem "BDSMGAmmo", 41
	Player.StartItem "SSGAmmo", 2
	Player.StartItem "RocketRounds", 6
	Player.StartItem "RailgunAmmo", 50
	Player.StartItem "HandGrenades", 1
	Player.StartItem "GrenadeAmmo", 2
	Player.StartItem "IsNOTTacticalClass", 1
	Player.StartItem "AmmoDroper"
	Species "Marines"

    +FRIGHTENING
}





// RED FLARE

Actor CrucibleRedFlare
{
    +NOINTERACTION
    +NOGRAVITY
    +CLIENTSIDEONLY
    +NOTONAUTOMAP
    renderstyle Add
    radius 1
    height 1
    alpha 0.4
    scale 0.4

    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,2)
        LENR A 2 bright
        stop
        TNT1 A 0
        LENR B 2 bright
        stop
    }
}

Actor CrucibleSpawnRedFlare : CrucibleRedFlare
{
    Alpha 0.2
    Xscale 0.25
    Yscale 0.15
    +FORCEXYBILLBOARD
}

Actor RedEnergySpawnRedFlare : CrucibleRedFlare
{
    Alpha 0.5
    Xscale 0.15
    Yscale 0.08
    +FORCEXYBILLBOARD
}





// WEAPON & AMMO SPAWNER

ACTOR BFG9000_Replacer : BFGReplacer {}
ACTOR BFG9000Replacer : RandomSpawner replaces BFG9000
{
    DropItem "BFG9000_Replacer", 255, 1
    DropItem "CrucibleSpawner", 255, 1
}

ACTOR AmmoCell2 : AmmoCell {}
ACTOR RedEnergyReplacer : RandomSpawner replaces AmmoCell
{
    DropItem AmmoCell2
    DropItem RedEnergyCharge
}

ACTOR CrucibleSpawner : CustomInventory
{
    Inventory.PickupSound "AXEDRAW"
    Inventory.Pickupmessage "You got something that looks too big to be called a sword!"
    Scale 0.6
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteAlways("BDDisableNewGuns", 0, 0, 0, 0)
        TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1, "Purist")

    Spawn2:
        TNT1 A 0 A_CheckSight("Spawn3")
        CRUC BBBBBB 2 A_SpawnItem("CrucibleSpawnRedFlare",0,10)
        Loop

    Spawn3:
        CRUC B 20
        Goto Spawn2

    Pickup:
        //TNT1 A 0 A_Log("Pickup state called!")
        TNT1 A 0 A_GiveInventory("TheCrucible", 1)
        TNT1 A 0 A_Giveinventory("RedEnergyCharge", random(3, 10))
        TNT1 A 1
        Stop

    Purist:
    Vanilla:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("BFG9000_Replacer")
        TNT1 A 1
        Stop
    }
}