ACTOR TheCrucible : BrutalWeapon {
    obituary "%o was choped down by %k's Crucible."
    Inventory.PickupSound "AXEDRAW"
    Inventory.Pickupmessage "You got something that looks too big to be called a sword!"
    Inventory.Icon "CRUCA0"

    DamageType Cut
    Tag "The Crucible"
    Weapon.SlotPriority 9999
    Weapon.SlotNumber 1
    Weapon.KickBack 100
    Weapon.AmmoUse 0
    Weapon.AmmoGive 0
    Weapon.AmmoType "RedEnergyCharge"
    Scale 0.4

    +WEAPON.NOAUTOAIM
    //+WEAPON.NOAUTOFIRE  // disable the alt fire
    +FORCEXYBILLBOARD
    +WEAPON.NOALERT
    +WEAPON.AXEBLOOD
    //+WEAPON.CHEATNOTWEAPON
    //+WEAPON.AMMO_OPTIONAL

    States
    {
    Spawn:
        TNT1 A 0 A_CheckSight("Spawn2")
        //CRUC B 10
        CRUC BBBBBB 2 //A_SpawnItem("CrucibleSpawnRedFlare",0,10)
        loop

    Spawn2:
        CRUC B 20
        Goto Spawn

    Ready:
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        TNT1 A 0 A_StartSound("crucible/raise", CHAN_AUTO)
        CRST A 15
        TNT1 A 0 A_Takeinventory("HasCutingWeapon",1)
        TNT1 A 0 A_TakeInventory("CrucibleAltFireToken",1)
        TNT1 A 0 A_TakeInventory("CrucibleNoChargeToken",1)
        CRST BCDEF 1
        TNT1 A 0 A_Quake(3, 5, 0, 32)
        Goto Ready2
    
    Ready2:
    Ready3:
        TNT1 A 0 A_JumpIfInventory("CrucibleAltFireToken", 1, 2)
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        TNT1 A 0 A_StartSound("crucible/idle", CHAN_AUTO)
        CRID ABCDE 2 A_WeaponReady
        TNT1 A 0 A_TakeInventory("CrucibleAltFireToken",1)
        TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
        loop

    Deselect:
        TNT1 A 0 A_TakeInventory("CrucibleAltFireToken",1)
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto Deselect2
        TNT1 A 0 A_StartSound("crucible/nocharge", CHAN_AUTO)
        CRST F 10
        CRST EDCB 1
        TNT1 A 0 A_Quake(3, 5, 0, 32)
        CRST A 10

    Deselect2:
        TNT1 AAAAAA 0 A_Lower
        CRST A 1 A_Lower
        loop

    Select:
        TNT1 A 0
        TNT1 A 0 A_Giveinventory("GoSpecial",1)
        TNT1 A 0 A_Takeinventory("FistsSelected",1)
        TNT1 A 0 A_Takeinventory("SawSelected",1)
        TNT1 A 0 A_Takeinventory("ShotgunSelected",1)
        TNT1 A 0 A_Takeinventory("SSGSelected",1)
        TNT1 A 0 A_Takeinventory("MinigunSelected",1)
        TNT1 A 0 A_Takeinventory("PlasmaGunSelected",1)
        TNT1 A 0 A_Takeinventory("RocketLauncherSelected",1)
        TNT1 A 0 A_Takeinventory("GrenadeLauncherSelected",1)
        TNT1 A 0 A_Takeinventory("BFGSelected",1)
        TNT1 A 0 A_Takeinventory("BFG10kSelected",1)
        TNT1 A 0 A_Takeinventory("RailGunSelected",1)
        TNT1 A 0 A_Takeinventory("SubMachineGunSelected",1)
        TNT1 A 0 A_Takeinventory("RevenantLauncherSelected",1)
        TNT1 A 0 A_Takeinventory("LostSoulSelected",1)
        TNT1 A 0 A_Takeinventory("FlameCannonSelected",1)
        TNT1 A 0 A_Takeinventory("HasBarrel",1)
        CRST A 1 A_Raise
        TNT1 AAAAAA 0 A_Raise
        loop

    Fire:
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, 1)
        Goto NoCharges
        CRAT A 1
        CRA2 A 1
        CRA2 B 1
        CRA2 C 1
        TNT1 A 0 A_Giveinventory("HasCutingWeapon",1)
        CRAT B 1
        CRAT G 1 A_FireProjectile("CrucibleAttack", 0, 0, 0, 0, 0, 0)
        CRAT HIJ 1
        CRAT K 1
        CRAT A 0 A_ReFire
        Goto Ready2
        
    AltFire:
        TNT1 A 0 A_JumpIfInventory("CrucibleAltFireToken",1,"AltFire2")
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 2, 1)
        Goto Ready2
        TNT1 A 0 A_TakeInventory("RedEnergyCharge", 2, 0)
        CRBL AB 1
        Goto AltFire2
        
    AltFire2:
        TNT1 A 0 A_GiveInventory("CrucibleAltFireToken",1)
        TNT1 A 0 A_StartSound("crucible/idle", CHAN_AUTO)
        TNT1 A 0 A_GiveInventory("CrucibleBlockPower",1)
        CRBL CDED 3
        Goto Ready2

    NoCharges:
        TNT1 A 0 A_JumpIfInventory("CrucibleNoChargeToken", 1, "NoCharges2")
        TNT1 A 0 A_TakeInventory("CrucibleAltFireToken",1)
        TNT1 A 0 A_GiveInventory("CrucibleNoChargeToken",1)
        TNT1 A 0 A_StartSound("crucible/nocharge", CHAN_AUTO)
        CRST F 10
        CRST EDCB 1
        TNT1 A 0 A_Quake(3, 5, 0, 32)
        CRST A 1

    NoCharges2:
        CRST A 15 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("Kicking",1,"DoKick")
        TNT1 A 0 A_JumpIfInventory("Taunting",1,"Taunt")
        TNT1 A 0 A_JumpIfInventory("Salute1", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("Salute2", 1, "Salute")
        TNT1 A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
        TNT1 A 0 A_JumpIfInventory("RedEnergyCharge", 1, "Ready")
        Loop
    }
}


// PROJECTILE

Actor CrucibleAttack {
    Projectile
    Radius 10
    Height 2
    DamageType Cut
    +MISSILE
    +FORCERADIUSDMG
    +EXTREMEDEATH
    +FORCEPAIN
    +NODAMAGETHRUST
    RenderStyle Add
    Alpha 0.6
    Damage 0
    Speed 0
    SeeSound "none"
    DeathSound "none"
    decal "none"
    Scale 0.01

    States {
        Spawn:
        Melee:
        Crash:
        Death:
            TNT1 A 0
            //TNT1 A 0 A_StartSound("crucible/strike", CHAN_AUTO)
            //TNT1 A 0 A_SpawnItem ("Sparks", 0)
            TNT1 A 10 A_JumpIf(A_Explode(random(200, 400), 60, 0, false, 60) > 0, "Hit")
            Stop
        
        Hit:
            TNT1 A 0
            TNT1 A 0 A_AlertMonsters(200)
            //TNT1 A 0 A_StartSound("weapons/gswing", CHAN_AUTO)
            TNT1 A 0 A_StartSound("crucible/strike", CHAN_AUTO)
            TNT1 A 0 A_TakeInventory("RedEnergyCharge", 1, 0, AAPTR_TARGET)
            Stop
    }
}


// AMMO

ACTOR RedEnergyChargeContainer : SwitchingDecoration
{
    Scale 0.35
    Activation THINGSPEC_Activate
    +BUMPSPECIAL
    +SPECIAL
    +FORCEXYBILLBOARD

    states
    {
    Spawn:
        AREN ABCABC 2 A_SpawnItem("RedEnergySpawnRedFlare", 0, 7)
        Loop
    Active:
        AREN D 0 A_SpawnItemEx("RedEnergyCharge")
        AREN D -1
        Stop
    }
}

Actor RedEnergyCharge : Ammo
{
    Inventory.PickupMessage "Picked up 3 red energy charge"
    Inventory.PickupSound "misc/p_pkup"
    Inventory.Amount 3
    Inventory.MaxAmount 30
    Inventory.Icon "ARENE0"
    Ammo.BackpackAmount 3
    Ammo.BackpackMaxAmount 3
    Scale 0.35

    +INVENTORY.UNDROPPABLE
    +INVENTORY.PERSISTENTPOWER
    +INVENTORY.KEEPDEPLETED
    +FORCEXYBILLBOARD

    states
    {
    Spawn:
        AREN ABCABC 2 A_SpawnItem("RedEnergySpawnRedFlare", 0, 7)
        loop
    }
}


// PLAYER

ACTOR TheTitanSlayer : BDoomer
{
    +FRIGHTENING
    Speed 1.0
    Health 200
    Player.DisplayName "The Titan Slayer"

    // startitem are not inherited
    Player.StartItem "TheCrucible"
    Player.StartItem "RedEnergyCharge", 30
    Player.StartItem "Rifle"
    Player.StartItem "Melee_Attacks"
    Player.StartItem "IsPlayer", 1
    Player.StartItem "Clip2", 30
    Player.StartItem "RifleAmmo", 31
    Player.StartItem "BDPistolAmmo", 16
    Player.StartItem "BDDualPistolAmmo", 16
    Player.StartItem "DoubleRifleAmmo", 31
    Player.StartItem "ShotgunAmmo", 10
    Player.StartItem "PlasmaAmmo", 50
    Player.StartItem "DoublePlasmaAmmo", 50
    Player.StartItem "AssaultShotgunAmmo", 20
    Player.StartItem "BDSMGAmmo", 41
    Player.StartItem "SSGAmmo", 2
    Player.StartItem "RocketRounds", 6
    Player.StartItem "RailgunAmmo", 50
    Player.StartItem "HandGrenades", 1
    Player.StartItem "GrenadeAmmo", 2
    Player.StartItem "IsNOTTacticalClass", 1
    Player.StartItem "AmmoDroper"
    Species "Marines"

    +FRIGHTENING
}


// RED FLARE

Actor CrucibleRedFlare
{
    +NOINTERACTION
    +NOGRAVITY
    +CLIENTSIDEONLY
    +NOTONAUTOMAP
    +FORCEXYBILLBOARD

    renderstyle Add
    radius 1
    height 1
    alpha 0.4
    scale 0.4

    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Jump(128,2)
        LENR A 2 bright
        stop
        TNT1 A 0
        LENR B 2 bright
        stop
    }
}

Actor CrucibleSpawnRedFlare : CrucibleRedFlare
{
    Alpha 0.2
    Xscale 0.25
    Yscale 0.15
}

Actor RedEnergySpawnRedFlare : CrucibleRedFlare
{
    Alpha 0.5
    Xscale 0.15
    Yscale 0.08
}


// TOKENS

ACTOR CrucibleNoChargeToken: Inventory
{
    inventory.maxamount 1
}
ACTOR CrucibleAltFireToken : Inventory
{
    Inventory.Maxamount 1
}
ACTOR CrucibleBlockPower : PowerupGiver
{
    +COUNTITEM
    +INVENTORY.AUTOACTIVATE
    +INVENTORY.ALWAYSPICKUP
    inventory.maxamount 0
    Powerup.Mode Reflective
    Powerup.Type Invulnerable
    Powerup.Duration 15
    ActiveSound "crucible4"
    SeeSound "sword3"
}


// WEAPON & AMMO SPAWNER

ACTOR BFG9000_Replacer : BFGReplacer {}
ACTOR BFG9000Replacer : RandomSpawner replaces BFG9000
{
    DropItem "BFG9000_Replacer", 255, 0
    DropItem "CrucibleSpawner", 255, 1
}

ACTOR CR_ClipAmmo : Clip2 {}
ACTOR RedEnergyChargeReplacer : RandomSpawner replaces Clip
{
    DropItem CR_ClipAmmo, 255, 10
    DropItem RedEnergyChargeContainer, 255, 2
}

ACTOR CR_ClipBoxAmmo : ClipBox2 {}
ACTOR RedEnergyChargeReplacer2 : RandomSpawner replaces ClipBox
{
    DropItem CR_ClipBoxAmmo, 255, 10
    DropItem RedEnergyChargeContainer, 255, 2
}

ACTOR CrucibleSpawner : CustomInventory
{
    Inventory.PickupSound "AXEDRAW"
    Inventory.Pickupmessage "You got something that looks too big to be called a sword!"
    Scale 0.4
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteAlways("BDDisableNewGuns", 0, 0, 0, 0)
        TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1, "Purist")

    Spawn2:
        TNT1 A 0 A_CheckSight("Spawn3")
        CRUC BBBBBB 2 //A_SpawnItem("CrucibleSpawnRedFlare",0,10)
        Loop

    Spawn3:
        CRUC B 20
        Goto Spawn2

    Pickup:
        //TNT1 A 0 A_Log("Pickup state called!")
        TNT1 A 0 A_GiveInventory("TheCrucible", 1)
        TNT1 A 0 A_Giveinventory("RedEnergyCharge", random(3, 10))
        TNT1 A 1
        Stop

    Purist:
    Vanilla:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("BFG9000_Replacer")
        TNT1 A 1
        Stop
    }
}